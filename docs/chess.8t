SMOOTHNESS=.1
CHEATMODE=1
SPC33="                                 "

DEF OWNPIECEAT(TOX, TOY, X, Y)=LEFT$(BOARD$(CHR$(TOX+64), TOY), 1)=LEFT$(BOARD$(CHR$(X+64), Y), 1)
DEF ISPIECEAT(TOX, TOY)=LEN(BOARD$(CHR$(TOX+64), TOY))<>0
DEF ONBOARD(TOX, TOY)=TOX>=1 AND TOX<=8 AND TOY>=1 AND TOY<=8

FOR L=0 TO 27:TEXTLINE$(L)="":N.
TEXTPRIO=33

TEXTLINE$(0)="  A  B  C  D  E  F  G  H   00:00"
TILEX=-16
TILEY=-16
FOR RANK=1 TO 8
  TEXTLINE$(26-3*RANK)=RANK+"                        "+RANK
  FOR FILE=1 TO 8
    TILE(RANK, FILE)=288*((RANK+FILE)MOD 2 + 14)
  N.
N.
TEXTLINE$(25)="  A  B  C  D  E  F  G  H   00:00"

TILE(8, 10)=288*14
SPRDGN("W")=DGNMAN():SPRX("W")=224:SPRY("W")=176
TILE(1, 10)=288*15
SPRDGN("B")=DGNMAN():SPRX("B")=224:SPRY("B")=8

DIM BOARD$(8,8)
DIM OFF$()
DIM PIECE$(16)
ENPASSANT=0
STEP=0
STARTED=0

DIM LEGALFROMFILE$(16)
DIM LEGALFROMY(16)
DIM LEGALTOFILE$(27)
DIM LEGALTOY(27)

DISPTIME=0
DIM PLAYERTIME(2)
PLAYERTIME(0)=0
PLAYERTIME(1)=0

FOR I=1 TO 8
  PIECE$(I)="P"+I
  C$=CHR$(64+I)
  FOR J=1 TO 8
    BOARD$(C$, J)=""
  N.
  BOARD$(C$, 2)="WP"+I
  BOARD$(C$, 7)="BP"+I
  SPRDGN("WP"+I)=2016
  SPRDGN("BP"+I)=3744
  'LEGALFROMFILE$(I-1)=C$:LEGALFROMY(I-1)=2
N.
BOARD$("A", 1)="WR1":BOARD$("H", 1)="WR2":BOARD$("A", 8)="BR1":BOARD$("H", 8)="BR2"
BOARD$("B", 1)="WN1":BOARD$("G", 1)="WN2":BOARD$("B", 8)="BN1":BOARD$("G", 8)="BN2"
BOARD$("C", 1)="WB1":BOARD$("F", 1)="WB2":BOARD$("C", 8)="BB1":BOARD$("F", 8)="BB2"
BOARD$("D", 1)="WQ":BOARD$("E", 1)="WK":BOARD$("D", 8)="BQ":BOARD$("E", 8)="BK"
SPRDGN("WR1")=1728:SPRDGN("WR2")=1728:SPRDGN("BR1")=3456:SPRDGN("BR2")=3456
PIECE$(9)="R1":PIECE$(10)="R2"
SPRDGN("WN1")=1440:SPRDGN("WN2")=1440:SPRDGN("BN1")=3168:SPRDGN("BN2")=3168
PIECE$(11)="N1":PIECE$(12)="N2"
SPRDGN("WB1")=1152:SPRDGN("WB2")=1152:SPRDGN("BB1")=2880:SPRDGN("BB2")=2880
PIECE$(13)="B1":PIECE$(14)="B2"
SPRDGN("WQ")=864:SPRDGN("WK")=576:SPRDGN("BQ")=2592:SPRDGN("BK")=2304
PIECE$(15)="K":PIECE$(16)="Q"
'LEGALFROMFILE$(8)="B":LEGALFROMY(8)=1
'LEGALFROMFILE$(9)="G":LEGALFROMY(9)=1

FOR I=1 TO 16
  SPRX("W"+PIECE$(I))=92:SPRX("B"+PIECE$(I))=92
  SPRY("W"+PIECE$(I))=92:SPRY("B"+PIECE$(I))=92
  SPRPRIO("W"+PIECE$(I))=2:SPRPRIO("B"+PIECE$(I))=2
N.

FOR I=0 TO 27
  SPRDGN(I)=4608
  SPRPRIO(I)=1
N.

SETTLED=0
DRAGGED$=""
GRABX=-1
MX=92:MY=92
LASTBOARDX=MX:LASTBOARDY=MY

STEPTIME=TIME()

FOR MLC=0 TO 1 STEP 0
  WAIT
  'CLOCK
  TMTODISP=INT((PLAYERTIME(STEP MOD 2)+TIME()-STEPTIME)/1000)
  IF TMTODISP=DISPTIME T.G.CLOCKEND
  DISPTIME=TMTODISP
  SEC$=RIGHT$("0"+TMTODISP MOD 60, 2)
  MIN$=RIGHT$("0"+INT(TMTODISP/60), 2)
  ROCL=25-(STEP MOD 2)*25 'ROW OF CLOCK
  TEXTLINE$(ROCL)=LEFT$(TEXTLINE$(ROCL), 27)+MIN$+":"+SEC$+MID$(TEXTLINE$(ROCL), 33, 16)
@CLOCKEND
  IF SETTLED=1 T.G. ANIMEND
  SETTLED=1
  FOR I=1 TO 8:FOR J=1 TO 8
    TARGETX=I*24-16
    TARGETY=200-J*24
    ANIMPC$=BOARD$(CHR$(I+64), J)
    IF ANIMPC$=DRAGGED$ AND GRABX<>-1 THEN ANIMPC$=""
    IF LEN(ANIMPC$)>0 THEN GOSUB ANIM
  N.:N.
  TARGETX=360
  TARGETY=0
  IF OFF$("length") = 0 T.G.OFFEND
  FOR I=0 TO OFF$("length")-1
    ANIMPC$=OFF$(I)
    GOSUB ANIM
    TARGETY=TARGETY+6
  N.
@OFFEND
@ANIMEND
  GET EVCODE
  IF EVCODE=27 THEN END

  MX=TOUCHX(1):MY=TOUCHY(1)
  IF MX<8 OR MX>=200 THEN GOTO UPDATEEND
  IF MY<8 OR MY>=200 THEN GOTO UPDATEEND
  LASTBOARDX=MX:LASTBOARDY=MY

@UPDATEEND
  X=INT(LASTBOARDX/24+.67)
  FILE$=CHR$(X+64)
  Y=INT((223-LASTBOARDY)/24)
  IF TEXTLINE$(26)<>FILE$+Y THEN TEXTLINE$(26)=FILE$+Y
  
  TOUCHED$=BOARD$(FILE$, Y)
  IF EVCODE=1 AND LEN(TOUCHED$)>0 THEN GOSUB PRESS
  IF EVCODE=-1 AND GRABX>-1 THEN GOSUB MOVE
  IF GRABX<>-1 THEN GOSUB DRAG
  
  'MARK MOVABLE PIECES
  IF EVCODE=0 AND STARTED=1 T.G.MARKEND
  STARTED=1
  IF GRABX=-1 T.G. MARKFROM
  'MARK LEGAL MOVE TARGETS
  FOR I=0 TO 27
    IF I>=LEGALTOFILE$("length") T.SPRX(I)=-24:N.:G.MARKEND

    SPRX(I)=ASC(LEGALTOFILE$(I))*24-1552
    SPRY(I)=200-LEGALTOY(I)*24
  N.
@MARKFROM
  FOR I=0 TO 15
    IF I>=LEGALFROMY("length") T.SPRX(I)=-24:N.:G.MARKEND
	
	SPRX(I)=ASC(LEGALFROMFILE$(I))*24-1552
    SPRY(I)=200-LEGALFROMY(I)*24
  N.
  G.MARKEND
@MARKEND
NEXT MLC

@ANIM
IF (SPRX(ANIMPC$)-TARGETX)^2+(SPRY(ANIMPC$)-TARGETY)^2 < 2 T.G. SNAP
SETTLED=0
SPRX(ANIMPC$)=SPRX(ANIMPC$)+(TARGETX-SPRX(ANIMPC$))*SMOOTHNESS
SPRY(ANIMPC$)=SPRY(ANIMPC$)+(TARGETY-SPRY(ANIMPC$))*SMOOTHNESS
R.
@SNAP
SPRX(ANIMPC$)=TARGETX
SPRY(ANIMPC$)=TARGETY
R.

@PRESS
IF CHEATMODE=1 T.G.CHEAT
FOR I=0 TO 15
IF I>=LEGALFROMFILE$("length") T.I=16:N.:R. 'END OF SET, NO MATCHES
IF TOUCHED$<>BOARD$(LEGALFROMFILE$(I), LEGALFROMY(I)) T.N. 'CONTINUE SEARCHING
IF I>15 T.R. 'END OF LOOP WITHOUT ANY MATCH
I=16:N. 'FOUND
@CHEAT
DRAGGED$=TOUCHED$
FROMFILE$=FILE$
FROMX=X
FROMY=Y
SORT$=MID$(DRAGGED$, 2, 1)
PLAYER=LEFT$(DRAGGED$, 1)="B"

@CALCPIECELIBERTY
DIM LEGALTOFILE$(27) 'CLEAR
DIM LEGALTOY(27)
LIBERTY=0
IF SORT$="N" THEN GOTO KNIGHT
IF SORT$="B" THEN GOTO BISHOP
IF SORT$="R" THEN GOTO ROOK
IF SORT$="Q" THEN GOTO QUEEN
IF SORT$="K" THEN GOTO KING
GOTO PAWN

@KNIGHT
TOX=X-2:TOY=Y-1
IF ONBOARD(TOX, TOY) T.GOSUB TESTOWN
TOX=X-1:TOY=Y-2
IF ONBOARD(TOX, TOY) T.GOSUB TESTOWN
TOX=X-2:TOY=Y+1
IF ONBOARD(TOX, TOY) T.GOSUB TESTOWN
TOX=X-1:TOY=Y+2
IF ONBOARD(TOX, TOY) T.GOSUB TESTOWN
TOX=X+2:TOY=Y-1
IF ONBOARD(TOX, TOY) T.GOSUB TESTOWN
TOX=X+1:TOY=Y-2
IF ONBOARD(TOX, TOY) T.GOSUB TESTOWN
TOX=X+2:TOY=Y+1
IF ONBOARD(TOX, TOY) T.GOSUB TESTOWN
TOX=X+1:TOY=Y+2
IF ONBOARD(TOX, TOY) T.GOSUB TESTOWN
GOTO LEGALTOEND

@BISHOP
GOSUB DIAGONAL
GOTO LEGALTOEND

@QUEEN
GOSUB DIAGONAL

@ROOK
DX=1:DY=0:GOSUB EXTENDX
DX=-1:DY=0:GOSUB EXTENDX
DX=0:DY=1:GOSUB EXTENDY
DX=0:DY=-1:GOSUB EXTENDY
GOTO LEGALTOEND

@PAWN
TOX=X+1:TOY=Y+1-2*PLAYER
IF ONBOARD(TOX, TOY) T.GOSUB TESTENPASSANT
TOX=X-1
IF ONBOARD(TOX, TOY) T.GOSUB TESTENPASSANT
TOX=X
IF ONBOARD(TOX, TOY) T.GOSUB TESTOWN
IF Y<>2+5*PLAYER T.G.LEGALTOEND
TOY=4+PLAYER
IF ONBOARD(TOX, TOY) T.GOSUB TESTOWN
GOTO LEGALTOEND

@KING
FOR TOY=Y-1 TO Y+1
  FOR TOX=X-1 TO X+1
    IF ONBOARD(TOX, TOY) T.GOSUB TESTOWN
N.:N.
IF X<>5 T.G.LEGALTOEND
IF Y<>1+7*PLAYER T.G.LEGALTOEND
TOY=Y
TOX=7
IF ISPIECEAT(6, Y) T.G.TESTOOB
IF ISPIECEAT(7, Y) T.G.TESTOOB
GOSUB ADDLEGALTO
@TESTOOB
TOX=2
IF ISPIECEAT(2, Y) T.G.LEGALTOEND
IF ISPIECEAT(3, Y) T.G.LEGALTOEND
IF ISPIECEAT(4, Y) T.G.LEGALTOEND
GOSUB ADDLEGALTO

@LEGALTOEND
GRABX=LASTBOARDX-SPRX(TOUCHED$)
GRABY=LASTBOARDY-SPRY(TOUCHED$)
SPRPRIO(TOUCHED$)=3
R.

@DRAG
SPRX(DRAGGED$)=LASTBOARDX-GRABX
SPRY(DRAGGED$)=LASTBOARDY-GRABY
R.

@MOVE
IF SORT$<>"P" OR X<>ENPASSANT OR Y<>6-PLAYER*3 T.G.CAPTUREENPEND
OLD$=BOARD$(FILE$, 5-PLAYER)
BOARD$(FILE$, 5-PLAYER)=""
G.CAPTURE
@CAPTUREENPEND

OLD$=BOARD$(FILE$, Y)
IF OLD$=DRAGGED$ T.G.DROP:REM J'ADOUBE
ENPASSANT=(SORT$="P" AND ABS(Y-FROMY)=2)*X

IF SORT$<>"K" OR Y<>1+7*PLAYER OR FROMFILE$<>"E" T.G.CASTLEEND
IF FILE$="B" T. BOARD$("C", Y)=BOARD$("A", Y):BOARD$("A", Y)="":G.XEND
IF FILE$="G" T. BOARD$("F", Y)=BOARD$("H", Y):BOARD$("H", Y)="":G.XEND
@CASTLEEND

IF LEN(OLD$)=0 T.G.XEND

@CAPTURE
OFF$(OFF$("length"))=OLD$
SPRPRIO(OLD$)=OFF$("length")+1
@XEND

PLAYERTIME(STEP MOD 2)=PLAYERTIME(STEP MOD 2)+TIME()-STEPTIME
MSG=STEP+1+". "+FROMFILE$+FROMY+FILE$+Y
TEXTLINE$(STEP MOD 26)=LEFT$(TEXTLINE$(STEP MOD 26) + SPC33, 33)+MSG
STEP=STEP+1
BOARD$(FROMFILE$, FROMY)=""
BOARD$(FILE$, Y)=DRAGGED$
STEPTIME=TIME()

@DROP
SETTLED=0
GRABX=-1
R.

@TESTENPASSANT
IF TOX=ENPASSANT AND TOY=6-PLAYER*3 T.G.ADDLEGALTO

@TESTENEMY
IF ISPIECEAT(TOX, TOY)=0 T.R.

@TESTOWN
IF OWNPIECEAT(TOX, TOY, X, Y) T.R.

@ADDLEGALTO
LEGALTOFILE$(LIBERTY)=CHR$(TOX+64)
LEGALTOY(LIBERTY)=TOY
LIBERTY=LIBERTY+1
R.

@EXTENDX
EX=INT(4.5+3.5*DX)
IF X=EX T.R.
TOY=Y+DY
FOR TOX=X+DX TO EX STEP DX
  IF ONBOARD(TOX, TOY)=0 T.TOX=EX+DX:N.:R.
  GOSUB TESTOWN
  IF ISPIECEAT(TOX, TOY) T.TOX=EX+DX
  TOY=TOY+DY
N.:R.

@EXTENDY
EY=INT(4.5+3.5*DY)
IF Y=EY T.R.
TOX=X+DX
FOR TOY=Y+DY TO EY STEP DY
  IF ONBOARD(TOX, TOY)=0 T.TOY=EY+DY:N.:R.
  GOSUB TESTOWN
  IF ISPIECEAT(TOX, TOY) T.TOY=EY+DY
  TOX=TOX+DX
N.:R.

@DIAGONAL
DX=1:DY=1:GOSUB EXTENDX
DX=1:DY=-1:GOSUB EXTENDX
DX=-1:DY=1:GOSUB EXTENDX
DX=-1:DY=-1:GOSUB EXTENDX
R.
